pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url = 'https://maven.minecraftforge.net' }
        //mavenLocal()
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '1.0.0' // https://plugins.gradle.org/plugin/org.gradle.toolchains.foojay-resolver-convention
}

rootProject.name = 'gitversion-gradle'

include 'common'
include 'changelog-gradle'

enableFeaturePreview 'TYPESAFE_PROJECT_ACCESSORS'

// Applying plugins causes them to not have any IDE support when also applied to any build.gradle files
// The workaround for now is to use this listener here so that it can stay in settings.gradle
// Note: The project will need to apply GradleUtils in order for these repositories to be added
// See: https://youtrack.jetbrains.com/issue/IDEA-332061/Gradle-Missing-Code-Completion-Suggestions-for-Settings-Plugins-in-Groovy-DSL
gradle.beforeProject { Project project ->
    project.pluginManager.withPlugin('net.minecraftforge.gradleutils') {
        project.repositories {
            mavenCentral()
            maven project.gradleutils.forgeMaven
            //mavenLocal()
        }
    }
}

dependencyResolutionManagement.versionCatalogs.register('libs') {
    version 'gradleutils', '3.3.21'

    plugin 'licenser',       'net.minecraftforge.licenser'    version '1.2.0' // https://plugins.gradle.org/plugin/net.minecraftforge.licenser
    plugin 'javadoc-links',  'io.freefair.javadoc-links'      version '8.14'  // https://plugins.gradle.org/plugin/io.freefair.javadoc-links
    plugin 'gradleutils',    'net.minecraftforge.gradleutils' versionRef 'gradleutils' // https://plugins.gradle.org/plugin/net.minecraftforge.gitversion
    plugin 'gitversion',     'net.minecraftforge.gitversion'  version '3.0.7' // https://plugins.gradle.org/plugin/net.minecraftforge.gitversion
    plugin 'changelog',      'net.minecraftforge.changelog'   version '3.0.6' // https://plugins.gradle.org/plugin/net.minecraftforge.changelog
    plugin 'plugin-publish', 'com.gradle.plugin-publish'      version '1.3.1' // https://plugins.gradle.org/plugin/com.gradle.plugin-publish
    plugin 'shadow',         'com.gradleup.shadow'            version '9.2.2' // https://plugins.gradle.org/plugin/com.gradleup.shadow

    // Static Analysis
    library 'nulls', 'org.jetbrains', 'annotations' version '26.0.2'

    // Gradle API
    // TODO [ForgeGradle][FG7][Gradle Api] REMOVE once Gradle publish their own API artifacts
    // Original:  https://github.com/remal-gradle-api/packages/packages/760197
    // Mirror:    https://maven.moddinglegacy.com/#browse/browse:maven-public:name%2Fremal%2Fgradle-api%2Fgradle-api%2F8.14.1
    version 'gradle', '9.0.0'
    library 'gradle', 'name.remal.gradle-api', 'gradle-api'  versionRef 'gradle'

    // GradleUtils
    // https://plugins.gradle.org/plugin/net.minecraftforge.gradleutils
    library 'gradleutils-shared', 'net.minecraftforge',             'gradleutils-shared'                           versionRef 'gradleutils'
    library 'gradleutils-plugin', 'net.minecraftforge.gradleutils', 'net.minecraftforge.gradleutils.gradle.plugin' versionRef 'gradleutils'

    // Git Version
    library 'gson', 'com.google.code.gson', 'gson' version '2.13.1'
}
