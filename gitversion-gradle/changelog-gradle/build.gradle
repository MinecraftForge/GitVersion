import com.github.jengelman.gradle.plugins.shadow.ShadowJavaPlugin
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.gradle.api.attributes.plugin.GradlePluginApiVersion

plugins {
    id 'java-gradle-plugin'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    alias libs.plugins.gradleutils
    alias libs.plugins.licenser
    alias libs.plugins.gitversion
    alias libs.plugins.changelog
    alias libs.plugins.plugin.publish
    alias libs.plugins.shadow
}

gradleutils.displayName = 'Git Changelog'
description = 'Creates a changelog text file based on git history using GitVersion.'
base.archivesName = 'changelog-gradle'
group = 'net.minecraftforge'
version = gitversion.tagOffset

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
    withJavadocJar()
}

configurations {
    // Applies the "Gradle Plugin API Version" attribute to configuration
    // This was added in Gradle 7, gives consumers useful errors if they are on an old version
    def applyGradleVersionAttribute = { Configuration configuration ->
        configuration.attributes {
            attribute(GradlePluginApiVersion.GRADLE_PLUGIN_API_VERSION_ATTRIBUTE, objects.named(GradlePluginApiVersion, gradleutils.unpack(libs.versions.gradle)))
        }
    }

    named(JavaPlugin.RUNTIME_ELEMENTS_CONFIGURATION_NAME, applyGradleVersionAttribute)
    named(ShadowJavaPlugin.SHADOW_RUNTIME_ELEMENTS_CONFIGURATION_NAME, applyGradleVersionAttribute)
}

dependencies {
    // Static Analysis
    compileOnly libs.nulls

    // Gradle API
    compileOnly libs.gradle

    // Common sources
    implementation projects.common

    // GradleUtils
    implementation libs.gradleutils.shared
}

// Removes local Gradle API from compileOnly. This is a workaround for bugged plugins.
// Publish Plugin: https://github.com/gradle/plugin-portal-requests/issues/260
// Shadow:         https://github.com/GradleUp/shadow/pull/1422
afterEvaluate { project ->
    project.configurations.named(JavaPlugin.COMPILE_ONLY_CONFIGURATION_NAME) { compileOnly ->
        compileOnly.dependencies.remove(project.dependencies.gradleApi())
    }
}

license {
    header = rootProject.file('../LICENSE-header.txt')
    newLine = false
    exclude '**/*.properties'
}

tasks.named('jar', Jar) {
    archiveClassifier = 'thin'
}

tasks.named('shadowJar', ShadowJar) {
    enableAutoRelocation = true
    archiveClassifier = null
    relocationPrefix = 'net.minecraftforge.gitversion.gradle.changelog.shadow'
}

tasks.withType(Javadoc).configureEach {
    javadocTool = javaToolchains.javadocToolFor { languageVersion = JavaLanguageVersion.of(24) }
}

changelog {
    fromBase()
    publishAll = false
}

gradlePlugin {
    website = gitversion.url
    vcsUrl = gitversion.url + '.git'

    plugins.register('changelog') {
        id = 'net.minecraftforge.changelog'
        implementationClass = 'net.minecraftforge.gitversion.gradle.changelog.ChangelogPlugin'
        displayName = gradleutils.displayName
        description = project.description
        tags = ['git', 'changelog']
    }
}

publishing {
    publications.register('pluginMaven', MavenPublication) {
        changelog.publish(it)
        gradleutils.promote(it)

        pom { pom ->
            name = gradleutils.displayName
            description = project.description

            gradleutils.pom.addRemoteDetails(pom)

            licenses {
                license gradleutils.pom.licenses.LGPLv2_1
            }

            developers {
                developer gradleutils.pom.developers.Jonathing
            }
        }
    }

    repositories {
        maven gradleutils.getPublishingForgeMaven(rootProject.file('../repo'))
    }
}
