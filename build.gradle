plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    alias libs.plugins.javadoc.links
    alias libs.plugins.licenser
    alias libs.plugins.gradleutils
    id 'net.minecraftforge.gitversion'
    id 'net.minecraftforge.changelog'
}

final projectDisplayName = 'Git Version'
final projectVendor = 'Forge Development LLC'
description = 'Used by MinecraftForge projects to calculate project versions based on Git history.'
base.archivesName = 'gitversion'
group = 'net.minecraftforge'
version = gitversion.tagOffset

println "Version: $version"

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
}

dependencies {
    // Static Analysis
    compileOnly libs.nulls

    // Git
    implementation libs.jgit

    // Config
    implementation libs.toml

    // JSON Output
    implementation libs.gson
}

license {
    header = file 'LICENSE-header.txt'
    newLine = false
    exclude '**/*.properties'
}

tasks.named('jar', Jar) {
    manifest {
        attributes([
            'Specification-Title'   : projectDisplayName,
            'Specification-Vendor'  : projectVendor,
            'Specification-Version' : gitversion.info.tag,
            'Implementation-Title'  : projectDisplayName,
            'Implementation-Vendor' : projectVendor,
            'Implementation-Version': project.version
        ], 'net/minecraftforge/gitver/api/')
    }
}

changelog {
    fromBase()
    publishAll = false
}

(components.java as AdhocComponentWithVariants).addVariantsFromConfiguration(evaluationDependsOn(':cli').configurations.getByName('shadowRuntimeElements')) { }

publishing {
    repositories {
        maven gradleutils.publishingForgeMaven
    }

    publications.register('mavenJava', MavenPublication) {
        from components.java

        changelog.publish(it)

        pom { pom ->
            name = projectDisplayName
            description = project.description

            gradleutils.pom.addRemoteDetails(pom)

            licenses {
                license gradleutils.pom.licenses.LGPLv2_1
            }

            developers {
                developer gradleutils.pom.developers.Jonathing
            }
        }
    }
}

idea.module { downloadSources = downloadJavadoc = true }
