import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    alias libs.plugins.licenser
    alias libs.plugins.gradleutils
    alias libs.plugins.shadow
}

final projectDisplayName = 'Git Version'
final projectVendor = 'Forge Development LLC'
description = 'Used by MinecraftForge projects to calculate project versions based on Git history.'
group = 'net.minecraftforge'
version = gitversion.tagOffset

println "Version: $version"

java {
    toolchain.languageVersion = JavaLanguageVersion.of 17
    withSourcesJar()
}

configurations {
    resolvable('shadowOnly') {
        description = 'Implementation dependencies that are not declared as required dependencies.'
    }

    named('compileOnly') {
        extendsFrom shadowOnly
    }

    named('runtimeClasspath') {
        extendsFrom shadowOnly
    }
}

dependencies {
    // Git
    implementation libs.jgit

    // Config
    implementation libs.toml

    // CLI
    shadowOnly libs.slf4j
    shadowOnly libs.jopt

    // Static Analysis
    compileOnly libs.nulls
}

license {
    header = file 'LICENSE-header.txt'
    newLine = false
    exclude '**/*.properties'
}

tasks.named('jar', Jar) {
    manifest {
        attributes([
            'Specification-Title'   : projectDisplayName,
            'Specification-Vendor'  : projectVendor,
            'Specification-Version' : gitversion.info.tag,
            'Implementation-Title'  : projectDisplayName,
            'Implementation-Vendor' : projectVendor,
            'Implementation-Version': project.version
        ], 'net/minecraftforge/gitver/api/')
    }

    exclude 'net/minecraftforge/gitver/cli/**'
}

tasks.named('shadowJar', ShadowJar) {
    manifest {
        attributes([
            'Main-Class': 'net.minecraftforge.gitver.cli.Main'
        ])
    }

    archiveClassifier = 'fatjar'
    minimize()
}

artifacts {
    archives shadowJar
}

changelog {
    fromBase()
    publishAll = false
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java

        artifactId = project.name
        changelog.publish it

        pom { pom ->
            name = projectDisplayName
            description = project.description

            gradleutils.pom.gitHubDetails = pom

            licenses {
                license gradleutils.pom.licenses.LGPLv2_1
            }

            developers {
                developer gradleutils.pom.developers.Jonathing
            }
        }
    }

    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

idea.module { downloadSources = downloadJavadoc = true }
