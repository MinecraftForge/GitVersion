pluginManagement {
    includeBuild('gradle-plugin') {
        name = 'gitversion-gradle'
    }

    repositories {
        gradlePluginPortal()
        maven { url = 'https://maven.minecraftforge.net' }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '1.0.0'
}

rootProject.name = 'gitversion'

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }

    //@formatter:off
    versionCatalogs.register('libs') {
        version 'gitversion-gradle', '3.0.0-beta.17'

        // Plugins
        plugin 'javadoc-links', 'io.freefair.javadoc-links'      version '8.14'
        plugin 'licenser',      'net.minecraftforge.licenser'    version '1.2.0'
        plugin 'gradleutils',   'net.minecraftforge.gradleutils' version '3.0.0-beta.20'
        //plugin 'gitversion',    'net.minecraftforge.gitversion'  versionRef 'gitversion-gradle' // https://plugins.gradle.org/plugin/net.minecraftforge.gitversion
        //plugin 'changelog',     'net.minecraftforge.changelog'   versionRef 'gitversion-gradle' // https://plugins.gradle.org/plugin/net.minecraftforge.changelog
        plugin 'shadow',        'com.gradleup.shadow'            version '9.0.1'

        // Git
        library 'jgit', 'org.eclipse.jgit', 'org.eclipse.jgit' version '7.2.0.202503040940-r'

        // Config
        library 'toml', 'org.tomlj', 'tomlj' version '1.1.1'

        // JSON Output
        library 'gson', 'com.google.code.gson', 'gson' version '2.13.1'

        // CLI
        library 'jopt',  'net.sf.jopt-simple', 'jopt-simple'  version '6.0-alpha-3'
        library 'slf4j', 'org.slf4j',          'slf4j-simple' version '1.7.36'

        // Static Analysis
        library 'nulls', 'org.jetbrains', 'annotations' version '26.0.2'
    }
    //@formatter:on
}
